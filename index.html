<html>
  <head>
    <link rel="stylesheet" type="text/css" href="style.css">
  </head>
  <body></body>
  <script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r71/three.min.js"></script>
  <script id="fragShader" type="shader-code">
    uniform vec2 resolution;
    uniform float time;
    uniform sampler2D texture;
    uniform sampler2D normalMap;
    uniform vec2 mousePos;

    void main() {
      // Get the position of this pixel.
      vec2 pos = gl_FragCoord.xy / resolution.xy;

      // Y mouse position, corrected for shader coordinates.
      float flippedY = resolution.y - mousePos.y;

      // Position our light where the mouse is.
      vec2 lightPos = vec2(mousePos.x / resolution.x, flippedY / resolution.y);

      // Define a radius for the light.
      float lightRadius = 0.85;
      float lightIntensity = 2.5;

      // Get the texture at the current pixel position.
      vec4 tex = texture2D(texture, pos);

      // Calculate how much light should reach the plane.
      vec3 normalVector = normalize(texture2D(normalMap, pos).xyz);
      normalVector.x -= 0.5;
      normalVector.y -= 0.5;

      float lightVectorX = (lightPos.x - pos.x) * resolution.x;
      float lightVectorY = (lightPos.y - pos.y) * resolution.y;
      vec3 lightVector = normalize(vec3(lightVectorX, lightVectorY, 100.0));

      // Calculate light diffusion from the normal and light vectors.
      float diffuse = dot( normalVector, lightVector );

      // Clamp the diffusion to positive values.
      diffuse = max(diffuse, 0.0);

      // Calculate the distance of this pixel from the light.
      float dist = distance(pos, lightPos);
      float falloff = (1. - dist/lightRadius);

      // Set a minimum brightness.
      float ambientLevel = 0.05;

      // Apply lighting. If this pixel would be black, use ambient lighting.
      gl_FragColor = max(tex * diffuse * lightIntensity * falloff, tex * ambientLevel);
    }
  </script>
  <script src="index.js"></script>
</html>
