<html>
  <head>
    <link rel="stylesheet" type="text/css" href="style.css">
  </head>
  <body></body>
  <script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r71/three.min.js"></script>
  <script id="fragShader" type="shader-code">
    uniform vec2 resolution;
    uniform float time;
    uniform sampler2D texture;
    uniform sampler2D normalMap;
    uniform vec2 mousePos;

    void main() {
      // Get the position of this pixel.
      vec2 pos = gl_FragCoord.xy / resolution.xy;

      // Y mouse position, corrected for shader coordinates.
      float flippedY = resolution.y - mousePos.y;

      // Position our light above the mouse.
      float lightHeight = 100.;
      vec3 lightPos = vec3( mousePos.x / resolution.x, flippedY / resolution.y, lightHeight );

      // Get the texture at the current pixel position.
      vec4 tex = texture2D( texture, pos );

      // Calculate how much light should reach the plane.
      vec3 normalVector = normalize(texture2D(normalMap, pos).xyz);
      // Ya just gotta do this. (See the tutorial for more info.)
      normalVector.x -= 0.5;
      normalVector.y -= 0.5;

      float lightVectorX = ( lightPos.x - pos.x ) * resolution.x;
      float lightVectorY = ( lightPos.y - pos.y ) * resolution.y;
      vec3 lightVector = normalize(vec3(lightVectorX, lightVectorY, lightPos.z));

      // Calculate light diffusion from the normal & light vectors.
      float diffuse = dot( normalVector, lightVector );

      // Clamp the diffusion to positive values.
      diffuse = max( diffuse, 0.0 );

      // Define other light properties.
      float lightRadius = 0.85;
      float lightIntensity = 2.5;

      // Calculate the distance of this pixel from the light.
      float dist = distance( pos, lightPos.xy );
      float falloff = ( 1. - dist/lightRadius );

      // Set a minimum brightness.
      float ambientLevel = 0.05;

      // Apply lighting. (If this pixel would be black, use ambient lighting.)
      gl_FragColor = max( tex * diffuse * lightIntensity * falloff, tex * ambientLevel );
    }
  </script>
  <script src="index.js"></script>
</html>
